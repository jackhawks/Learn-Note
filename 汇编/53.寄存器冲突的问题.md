# 寄存器冲突的问题



## 引子



-   问题：编程将 data 段中的字符串转化为大写



```assembly
assume cs:code

data segment
    db 'conversation'
data ends

code segment
    start:mov ax,data
          mov ds,ax
          mov si,0
          mov cx,12       ; 问题: 这里的 12 是怎么来的???????????
          call capital
          mov ax,4c00h
          int 21h
          
  capital:and byte ptr [si],11011111b
          inc si
          loop capital
          ret
code ends
end start
```



### 代码：编程将 data 段中的字符串转化为大写



```assembly
assume cs:code

data segment
    db 'conversation'
data ends

code segment



;------------------------------------------------------↓; 设置字符串的起始地址,并调用子程序
    start:mov ax,data
          mov ds,ax
          mov si,0
          mov cx,12
          call capital
;------------------------------------------------------↑;



          mov ax,4c00h
          int 21h



;------------------------------------------------------↓; 子程序依次读取每个字符进行检测,如果不是 0 ,进行大写的转,如果是 0 ,结束处理.
                                                        ; ----- 不在需要字符串的长度作为参数
                                                        ; ps:实用性更好的子程序
  capital:mov cl,[si]
          mov ch,0
          jcxz ok
          and byte ptr [si],11011111b
          inc si
          jmp short capital
       ok:ret
;------------------------------------------------------↑;



code ends
end start
```



#### 再例：将以下字符串转为大写



```assembly
assume cs:code

data segment
    db 'word',0
    db 'unix',0
    db 'wind',0
    db 'good',0
data ends
```

```assembly
code segment
    start:mov ax,data
          mov ds,ax
          mov bx,0
          mov cx,4
        s:mov si,bx
          call capital
          add bx,5
          loop s
          mov ax,4c00h
          int 21h
          
  capital:mov cl,[si]
          mov ch,0
          jcxz ok
          and byte ptr [si],11011111b
          inc si
          jmp short capital    
       ok:ret
code ends
end start
```

>   [!CAUTION]
>
>   上面的程序 cx 既用于循环，又用于读取数据   --------  冲突了！！！！！！！！！！！！







## 寄存器冲突问题的解决



-   有两种可能的解决方案

    1.  在编写调用子程序的程序时，注意看看子程序中有没有用到会产生冲突的寄存器

        -   如果有，调用者使用别的寄存器

        >   问题：
        >
        >   1.调用子程序的程序会很麻烦，必须要小心检查所调用的子程序中是否有将要产生冲突的寄存器
        >
        >   2.要调用子程序，需要要看到子程序的源码！！！！！！

    2.  在编写子程序的时候，不要再使用会产生冲突的寄存器

        >   子程序应该时独立的，编写子程序的时候无法知道也不必知道将来的调用情况

-   我们希望

    1.  编写调用子程序程序的时候不需要关心子程序中到底使用了哪些寄存器
    2.  编写子程序的时候不需要关心调用者使用了哪些寄存器
    3.  不会发生寄存器冲突







>   [!NOTE]
>
>   子程序的标准框架
>
>   ```assembly
>   ;子程序开始: 子程序中使用的寄存器入栈
>   ;          子程序内容
>   ;          子程序使用的寄存器出栈
>   ;          返回 ( ret、retf )
>   ```
>
>   可行的解决方案：在子程序的开始，将要用到的所有寄存器中的内容都保存起来，在子程序返回前再恢复。



## 寄存器冲突问题的解决示例



```assembly
assume cs:code

data segment
    db 'word',0
    db 'unix',0
    db 'wind',0
    db 'good',0
data ends

code segment
    start:mov ax,data
          mov ds,ax
          mov bx,0
          mov cx,4
        s:mov si,bx
          call capital
          add bx,5
          loop s
          mov ax,4c00h
          int 21h



          ; 子程序
;------------------------------------------------------↓; 子程序中使用的寄存器入栈
  capital:push cx
          push si
;------------------------------------------------------↑;



   change:mov cl,[si]
          mov ch,0
          jcxz ok
          and byte ptr [si],11011111b
          inc si
          jmp short change



;------------------------------------------------------↓; 子程序使用的寄存器出栈
       ok:pop si
          pop cx
;------------------------------------------------------↑;



          ret
code ends
end start
```



