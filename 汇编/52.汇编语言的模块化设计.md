# 汇编语言的模块化设计



## 模块化程序设计



 ```assembly
 assume cs:code
 
 code segment
     main:...
          call sub1
          ...
          mov ax,4c00h
          int 21h
          
     sub1:...
          call sub2
          ...
          ret
          
     sub2:...
          ...
          ret
 code ends
 end main
 ```



```mermaid
graph TB
    A(main) -- call --> B(sub1)
    B(sub1) -- call --> C(sub2)
    
    C -. ret .-> B
    B -. ret .-> A
```



-   调用子程序：`call 指令`
-   返回：`ret 指令`
-   子程序：根据提供的**参数**处理一定的事务，处理后，将**结果**（返回值）提供给调用者。



## 参数和结果传递的问题



-   问题：根据提供的 N，计算 N 的 3 次方
-   考虑
    1.  我们将参数 N 存储在什么地方？
    2.  计算得到的值，存储在什么地方？
-   方案
    1.  用寄存器传递参数
    2.  用内存单元进行参数传递
    3.  用栈传递参数



```assembly
assume cs:code

code segment
    main:...
         call sub1
         ...
         mov ax,4c00h
         int 21h
         
    sub1:...
         call sub2
         ...
         ret
         
    sub2:...
         ...
         ret
code ends
end main
```

```c
#include <stdio.h>

int cube(int x);

int main(){
    printf("%d\n", cube(2));
    return 0;
}

int cube(int x){
    int f;
    f = x * x;
    f = f * x;
    return f;
}
```



### 1.用寄存器来存储参数和结果是最常用的方法



-   问题：根据提供的 N，计算 N 的 3 次方

-   考虑

    1.  我们将参数 N 存储在什么地方？
    2.  计算得到的值，存储在什么地方？

-   用寄存器传递参数

    -   参数放到 bx 中，即 `(bx)=N`
    -   子程序中用多个 mul 指令计算 N^3
    -   将结果放到 dx 和 ax 中：`(dx:ax)=N^3`
        -   *如果需要传递的数据有 3 个、4 个或更多，寄存器不够了，怎么办？？？？？？？？？*

    ```assembly
    ; 汇编子程序
    
    cube:mov ax,bx
         mul bx
         mul bx
         ret
    ```

    

#### 编程任务：计算 data 段中第一组数据的 3 次方，结果保存在后面一组 dword 单元中。



```assembly
assume cs:code

data segment
    dw 1,2,3,4,5,6,7,8
    dd 0,0,0,0,0,0,0,0
data ends

code segment
    start:mov ax,data
          mov ds,ax
          mov si,0
          mov di,16
          
          ; 循环处理
          mov cx,8
        s:mov bx,[si]
          call cube
          mov [di],ax
          mov [di].2,dx
          add si,2
          add si,4
          loop s
          
          ; 子程序
     cube:mov ax,bx
          mul bx
          mul bx
          ret
          
          mov ax,4c00h
          int 21h
code ends
end start
```

```bash
c:\>debug p10-5.exe
-d
076A:0000    01 00 02 00 03 00 04 00-05 00 06 00 07 00 08 00
076A:0010    01 00 00 00 08 00 00 00-1B 00 00 00 40 00 00 00
076A:0020    7D 00 00 00 D8 00 00 00-57 01 00 00 00 00 00 00
```





### 2.用内存单元批量传递数据



-   方案
    -   将批量数据放到内存中，然后将它们所在的内存空间的首地址放在寄存器中，传递给所需要的子程序
    -   对于具有批量数据的返回结果，也可用同样的方法
-   编程
    -   将 data 段中的字符串转化为大写



```assembly
assume cs:code

data segment
    db 'conversation'
data ends

code segment

    start:mov ax,data
          mov ds,ax
          mov si,0
          mov cx,12
          call capital
          mov ax,4c00h
          int 21h
          
          ; 子程序
  capital:and byte ptr [si],11011111b
          inc si
          loop capital
          ret
code ends
end start
```

```bash
c:\>debug p10-6.exe
-d  076a:0 f
076A:0000  43 4F 4E 56 45 52 41-54 49 4F 4E 00 00 00 00     CONVERSATION....
```



### 3.用栈传递参数



-   原理：由调用者将需要传递给子程序的参数压入栈中，子程序从栈中取得参数

-   任务：计算 `(a-b)^3`，a、b 为 word 型数据

    -   进入子程序前，参数 a、b 入栈
    -   调用子程序，将使栈顶存放 IP
    -   结果：`(dx:ax)=(a-b)^3`

-   例：设 `a = 3,b = 1`，计算：`(a-b)^3`

    -   ```assembly
        mov ax,1
        push ax
        mov ax,3
        push ax
        call difcube
        ```

    -   栈中的结构

        ```assembly
        ;|BP旧值  |     ; 栈顶
        ;|返回点IP|
        ;|a      |
        ;|b      |
        ```

    -   ```assembly
        difcube:push bp
                mov bp,sp
                mov ax,[bp+4]    ; 将栈中 a 的值送入 ax 中
                sub ax,[bp+6]    ; 减栈中 b 的值
                mov bp,ax
                mul bp
                mul bp
                pop bp
                ret 4
        ```



>   [!NOTE]
>
>   指令 ret n 的含义
>
>   ```assembly
>   pop ip
>   add sp,n
>   ```



#### 程序的执行过程中栈的变化



```assembly
;------------------------------------------------------↓; (1) 栈的初始情况
;------------------------------------------------------↑; 1000:0000   00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00
code segment



;------------------------------------------------------↓; (2) 调用子程序前的栈
                                                        ;                                                 | a | | b |
                                                        ; 1000:0000   00 00 00 00 00 00 00 00 00 00 00 00 03 00 01 00
    start:mov ax,1
          push ax
          mov ax,3
          push ax
;------------------------------------------------------↑;



;------------------------------------------------------↓; (3) 调用子程序,IP 入栈
                                                        ;                                           |IP|  | a | | b |
                                                        ; 1000:0000   00 00 00 00 00 00 00 00 00 00 XX XX 03 00 01 00
                                                        ;                                           ↑
                                                        ;                                         ss:sp
          call difcube
;------------------------------------------------------↑;


          mov ax,4c00h
          int 21h


;------------------------------------------------------↓; (4) 在栈中保存 bp 的旧值
                                                        ;                                     |bp | |IP | | a | | b |
                                                        ; 1000:0000   00 00 00 00 00 00 00 00 XX XX XX XX 03 00 01 00
                                                        ;                                     ↑
                                                        ;                                   ss:sp
  difcube:push bp
;------------------------------------------------------↑;


;------------------------------------------------------↓; (5) 从栈中获得参数并计算;计算的结果(返回值)在 dx 和 ax 中
          mov bp,sp
          mov ax,[bp+4]
          sub ax,[bp+6]
          mov bp,ax
          mul bp
          mul bp
;------------------------------------------------------↑;


;------------------------------------------------------↓; (6) 恢复在栈中保存的 bp 值
                                                        ;                                           |IP | | a | | b |
                                                        ; 1000:0000   00 00 00 00 00 00 00 00 XX XX XX XX 03 00 01 00
                                                        ;                                           ↑
                                                        ;                                         ss:sp
          pop bp
;------------------------------------------------------↑;


;------------------------------------------------------↓; (7) 返回后,放弃入栈参数
                                                        ; 1000:0000   00 00 00 00 00 00 00 00 XX XX XX XX 03 00 01 00
                                                        ;                                                             ↑
                                                        ;                                                           ss:sp

          ret 4
;------------------------------------------------------↑;

code ends
end start
```





## 小结：参数和结果传递的问题



-   问题：根据提供的 N，计算 N 的 3 次方
-   考虑
    1.  我们将参数 N 存储在什么地方？
    2.  计算得到的值，存储在什么地方？
-   方案
    1.  用寄存器传递参数
        -   局限是只能够有几个有限的数据
    2.  用内存单元进行参数传递
        -   通过内存单元的首地址通过寄存器传递给子程序
    3.  用栈传递参数
        -   在传递过程中对栈的使用有一些技巧

