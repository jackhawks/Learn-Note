# jmp指令



## jmp指令-------无条件转移



-   jmp 指令的功能
    -   无条件转移，可以只修改 IP，也可以同时修改 IP 和 CS。
-   jmp 指令要给出两种信息
    -   转移的目标地址
    -   转移的距离
        -   **段间转移（远转移）：** `jmp 2000:1000`
        -   **段内短转移：** `jmp short` 标号；IP 的修改范围为：`-128~127` ，8 位的位移。
        -   **段内近转移： ** `jmp near ptr` 标号；IP 的修改范围为：`-32768~32767` ，16 位的位移。





## jmp指令：依据位移进行转移



-   引子：常见指令中的立即数均在机器指令中有体现

    -   ```bash
        -a 073f:0100
        073F:0100 mov ax, 0123
        073F:0103 mov ax, [0123]
        073F:0106 push [0123]
        073F:010A
        -u 073f:0100
        073F:0100 B82301                 MOV     AX,0123
        073F:0100 A12301                 MOV     AX,[0123]
        073F:0103 FF362301               PUSH    [0123]
        ```

-   问题：`jmp short` 指令中，转移到哪里了？

    -   示例1

        -   ```assembly
            assume cs:codesg
            
            codesg segment
                start:mov ax,0
                      jmp short s
                      add ax,1
                    s:inc ax
            codesg ends
            end start
            ```

        -   ```bash
            C:\>debug p9-2.exe
            -u
            076A:0000 B80000                 MOV     AX,0000
            076A:0003 EB03                   JMP     0008
            076A:0005 050100                 ADD     AX,0001
            076A:0008 40                     INC     AX
            ```

    -   示例2

        -   ```assembly
            assume cs:codesg
            
            codesg segment
                start:mov ax,0
                      jmp short s
                      add ax,1
                      nop
                      nop
                    s:inc ax
            codesg ends
            end start
            ```

        -   ```bash
            C:\>debug p9-3.exe
            -u
            076A:0000 B80000                 MOV     AX,0000
            076A:0003 EB05                   JMP     000A
            076A:0005 050100                 ADD     AX,0001
            076A:0008 90                     NOP
            076A:0009 90                     NOP
            076A:000A 40                     INC     AX
            ```

    -   `jmp short` 的机器指令中，包含的是跳转到指令的相对位置，而不是转移的目标地址。

    -   上面示例程序 `jmp short s` 指令的读取和执行

        1.   `(IP)=0003` ，`CS:IP` 指向 `EB 05` （jmp 的机器码）
        2.  读取指令码 EB 05 进入指令缓冲器
        3.   `(IP)= (IP)+所读取指令的长度=(IP)+2=0005`，`CS:IP` 指向 `add ax,0001`
        4.  CPU 执行指令缓冲器中的指令 `EB05`
        5.  指令 `EB 05` 执行后，`(IP)=(IP)+05=000AH`，`CS:IP` 指向 `inc ax`



## 两种段内转移



-   **短转移：** `jmp short 标号`

    -   功能：`(IP)=(IP)+8位位移`

    -   原理

        1.  8 位位移 = “标号” 处的地址 - jmp 指令后的第一个字节的地址
        2.  short 指明此处的位移为 8 位位移
        3.  8 位位移的范围为：-128~127，用补码表示
        4.  8 位位移由编译程序在编译时算出

    -   *转移位移超界！！！*

        -   ```assembly
            assume cs:codesg
            
            codesg segment
                start:jmp short s
                      db 123 dup (0)
                    s:mov ax,0ffffh
            codesg ends
            end start
            ```

        -   ```bash
            C:\>masm p9-error
            ...
            p9-error.ASM(3): error A2053: Jump out of range by 1 byte(s) // 转移位移超界！！！
            ...
                  0 Warning Errors
                  1 Servere Errors
            ```

-   **近转移：** 指令 `jmp near ptr 标号`

    -   功能：`(IP)=(IP)+16位位移`
    -   原理
        1.  16 位位移 = “标号” 处的地址 - jmp 指令后的第一个字节的地址
        2.  near ptr 指明此处的位移为 16 位位移，进行的是段内近转移
        3.  16 位位移的范围为：-32769~32767，用补码表示
        4.  16 位位移由编译程序在编译时算出



## 远转移：jmp far ptr 标号



-   远转移 `jmp far ptr 标号`

    -   **段间转移**

        -   far ptr 指明了跳转到的目的地址，即包含了标号的段地址 CS 和偏移地址 IP

        -   ```assembly
            assume cs:codesg
            
            codesg segment
                start:mov ax,0
                      mov bx,0
                      jmp far ptr s
                      db 256 dup (0)
                    s:add ax,1
                      inc ax
            codesg ends
            end start
            ```

        -   ```bash
            C:\>debug p9-5.exe
            -u
            076A:0000 B80000                 MOV     AX,0000
            076A:0003 BB0000                 MOV     BX,0000
            076A:0006 EA0B016A07             JMP     076A:010B
            076A:000B 0000                   ADD     [BX+SI],AL
            ```

        -   ```bash
            -u 109
            076A:0109 0000                   ADD     [BX+SI],AL
            076A:010B 050100                 ADD     AX,0001
            076A:010E 40                     INC     AX
            ```

-   近转移 `jmp near ptr 标号`

    -   **段内转移**

        -   near ptr 指明了相对于当前 IP 的转移位移，而不是转移的目的地址

        -   ```assembly
            assume cs:codesg
            
            codesg segment
                start:mov ax,0
                      mov bx,0
                      jmp near ptr s
                      db 256 dup (0)
                    s:add ax,1
                      inc ax
            codesg ends
            end start
            ```

        -   ```bash
            C:\>debug p9-4.exe
            -u
            076A:0000 B80000                 MOV     AX,0000
            076A:0003 BB0000                 MOV     BX,0000
            076A:0006 E90001                 JMP     0109
            076A:0009 0000                   ADD     [BX+SI],AL
            ```

        -   ```bash
            -u 109
            076A:0109 050100                 ADD     AX,0001
            076A:010C 40                     INC     AX
            ```



## 转移地址在寄存器中的 jmp 指令



-   指令格式：`jmp 16位寄存器`

    -   功能：IP = (16位寄存器)

    -   例子

        -   jmp ax
        -   jmp bx

        ```assembly
        assume cs:codesg
        
        codesg segment
            start:mov ax,0
                  mov bx,ax
                  jmp bx
                  mov ax,0123H
        codesg ends
        end start
        ```

        ```bash
        C:\>debug p9-6.exe
        -u
        076A:0000 B80000                 MOV     AX,0000
        076A:0003 8BD8                   MOV     BX,AX
        076A:0005 FFE3                   JMP     BX
        076A:0007 B82301                 MOV AX,0123
        ```



## 转移地址在内存中的 jmp 指令



-   `jmp word ptr` 内存单元地址

    -   **段内转移**

        -   功能：从内存单元地址处开始存放一个**字**，是转移的目的**偏移地址**

        ```assembly
        mov ax,0123H
        mov ds:[0],ax
        jmp word ptr ds:[0]
        
        ; 执行后, (IP)=0123H
        ```

        ```assembly
        mov ax,0123H
        mov [bx],ax
        jmp word ptr [bx]
        
        ; 执行后, (IP)=0123H
        ```

-   `jmp dword ptr` 内存单元地址

    -   **段间转移**

        -   功能：从内存单元地址处开始存放着**两个字**，高地址处的字是转移的**目的段地址**，低地址处是转移的目的**偏移地址**

        ```assembly
        mov ax,0123H
        mov ds:[0],ax
        mov word ptr ds:[2],0
        jmp dword ptr ds:[0]
        
        ; 执行后:
        ; (CS)=0
        ; (IP)=0123H
        ; CS:IP 指向 0000:0123
        ```

        ```assembly
        mov ax,0123H
        mov [bx],ax
        mov word ptr [bx+2],0
        jmp dword ptr [bx]
        
        ; 执行后:
        ; (CS)=0
        ; (IP)=0123H
        ; CS:IP 指向 0000:0123
        ```

        

## jmp 指令小结



| jmp 指令格式                 | 示例                                                         |
| ---------------------------- | ------------------------------------------------------------ |
| jmp 标号                     | 段间转移（远转移）：jmp far ptr 标号<br>段内短转移：jmp short 标号；8 位的位移<br>段内近转移：jmp near ptr 标号；16位的位移 |
| jmp 寄存器                   | jmp bx；16位的位移                                           |
| jmp 内存单元(表示跳转到地址) | 段内转移：jmp word ptr 内存单元地址；jmp word ptr [bx]<br>段间转移：jmp dword ptr 内存单元地址；jmp dword ptr [bx] |



>   [!CAUTION]
>
>   在源程序中，不允许使用 `jmp 2000:0100` 的转移指令实现段间转移
>
>   -   这是在 Debug 中使用的汇编指令，汇编编译器并不认识
>   -   如果在源程序中使用，编译时也会报错！！！

