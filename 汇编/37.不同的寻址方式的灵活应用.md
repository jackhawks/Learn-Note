# 不同的寻址方式的灵活应用



## 对内存的寻址方式



| 形式            | 名称             | 特点                         | 意义                                                 | 示例                            |
| :-------------- | :--------------- | :--------------------------- | :--------------------------------------------------- | :------------------------------ |
| `[idata]`       | 直接寻址         | 用一个常量/立即数来表示地址  | 用于直接定位一个内存单元                             | `mov ax,[200]`                  |
| `[bx]`          | 寄存器间接寻址   | 用一个变量来表示内存地址     | 用于间接定位一个内存单元                             | `mov bx,0`<br>`mov ax,[bx]`     |
| `[bx+idata]`    | 寄存器相对寻址   | 用一个变量和常量表示地址     | 可以在一个起始地址的基础上用变量间接定位一个内存单元 | `mov bx,4`<br>`mov ax,[bx+200]` |
| `[bx+si]`       | 基址变址寻址     | 用两个变量表示地址           | /                                                    | `mov ax,[bx+si]`                |
| `[bx+si+idata]` | 相对基址变址寻址 | 用两个变量和一个常量表示地址 | /                                                    | `mov ax,[bx+si+200]`            |



## 案例1：灵活应用不同的寻址方式



-   问题：编程将 datasg 段中每个单词的头一个字母改为大写字母

    -   ```assembly
        assume cs:codesg,ds:datasg
        
        datasg segment
            db '1.file    '
            db '2.edit    '
            db '3.search  '
            db '4.view    '
            db '5.options '
            db '6.help    '
        datasg ends
        
        codesg segment
          start:...
                mov 4c00h
                int 21h
        codesg ends
        end start
        ```

        >   R = 第一行的地址
        >
        >   mov cx,6
        >
        >   
        >
        >   s:改变 R 行 3 列的字母为大写
        >
        >   R = 下一行的地址
        >
        >   loop s

        ```assembly
            mov ax,datasg
            mov ds,ax
            
            mov bx,0
            mov cx,6
        
          s:mov al,[bx+3]
            and al,11011111b
            mov [bx+3],al
            add bx,16
            loop s
        ```





## 案例2：灵活应用不同的寻址方式



-   问题：编程将 datasg 段中每个单词改为大写字母

    -   ```assembly
        assume cs:codesg,ds:datasg
        
        datasg segment
            db 'ibm     '
            db 'dec     '
            db 'dos     '
            db 'vax     '
        datasg ends
        
        codesg segment
            start:...
        codesg ends
        end start
        ```

        

        >   [!NOTE]
        >
        >   -   4个字符串，看成一个4行16列的二维数组
        >   -   要修改二维数组的每一行的前3列
        >   -   构造4x3次的二重循环

        

        >   [!TIP]
        >
        >   使用 [bx+si] 方式

        

        ```assembly
            R = 第一行的地址
            mov cx,4
        
        s0: C = 第一列的地址
            mov cx,3
        
        s:  改变 R 行,C 列字母为大写
            C = 下一列的地址
            loop s
            R = 下一行的地址
            loop s0
        ```

        ```assembly
        ; 有问题的代码
        ; 缺陷: 循环次数由 cx 定,可是 cx 只有一个
            mov ax,datasg
            mov ds,ax
            mov bx,0
            mov cx,4             ; !!!
         s0:mov si,0
            mov cx,3             ; !!!
          s:mov al,[bx+si]
                and al,11011111b
                mov [bx+si],al
                inc si
                loop s            ; !!!
            add bx,16
            loop s0               ; !!!
        ```

        

        **二重循环问题的处理------方法1**

        

        问题：编程将 datasg 段中的每个单词改为大写

        

        ```assembly
        ; 也有问题 !!!!!!
        ; 如果 dx 已经被用了,别的寄存器也有用处了呢?寄存器只有14个!!!
            mov ax,datasg
            mov ds,ax
            mov bx,0
            mov cx,4
         s0:mov dx,cx             ; 将外层循环的 cx 值保存在 dx 中
            mov si,0
            mov cx,3              ; cx 设置为内层循环的次数
          s:mov al,[bx+si]
                and al,11011111b
                mov [bx+si],al
                inc si
                loop s
            add bx,16
            mov cx,dx             ; 用 dx 中存放的外层循环的计数值恢复 cx
            loop s0               ; (cx)  = (cx) - 1 针对外层循环
        ```

        

        **二重循环问题的处理------~~方法2 (不推荐做法)~~**

        

        ~~用固定的内存空间保存数据~~

        

        ```assembly
            mov ax,datasg
            mov ds,ax
            mov bx,0
            mov cx,4
         s0:mov ds:[40H],cx        ; 将外层循环的 cx 值保存在 datasg:40H 单元中
            mov si,0
            mov cx,3               ; cx 设置为内层循环的次数
          s:mov al,[bx+si]
                and al,11011111b
                mov [bx+si],al
                inc si
                loop s
            add bx,16
            mov cx,ds:[40H]        ; 用 datasg:40H 单元中的值恢复 cx
            loop s0
        ```

        

        **二重循环问题的处理------方法3**

        

        用栈保存数据

        

        ```assembly
        stacksg segment
            dw 0,0,0,0,0,0,0,0
        stacksg ends
        ```

        ```assembly
            mov ax,stacksg
            mov ss,ax
            mov sp,16
            mov ax,datasg
            mov ds,ax
            mov bx,0
            mov cx,4
         s0:push cx                ; 将外层循环的 cx 值压栈
            mov si,0
            mov cx,3               ; cx 设置为内层循环的次数
          s:mov al,[bx+si]
                and al,11011111b
                mov [bx+si],al
                inc si
                loop s
            add bx,16
            pop cx                 ; 从栈顶弹出原 cx 的值,恢复 cx
            loop s0
        ```